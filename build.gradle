plugins {
    id "groovy"
}

group = "com.pg"
version = "1.0.0"

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://repo.jenkins-ci.org/releases/' }
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.6'
    implementation 'org.jenkins-ci.main:jenkins-core:2.270'
    implementation 'com.cloudbees:groovy-cps:1.30'

    testImplementation group: 'com.lesfurets', name: 'jenkins-pipeline-unit', version: '1.1'
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src', 'vars']
        }
    }

    test {
        groovy {
            srcDirs = ['test']
        }
    }
}
test {

    // delete old test reports
    dependsOn cleanTest

    // don't stop if tests fail
    ignoreFailures = true

    useJUnit {

    }

    // minimize logging
    testLogging.maxGranularity = 0

    // show stdout from tests
    onOutput { dest, event -> print event.message }

    // show test results
    def results = []
    afterTest { desc, result ->
        println "${desc.className.split("\\.")[-1]}: " +
                "${desc.name}: ${result.resultType}"
    }
    afterSuite { desc, result ->
        if (desc.className) { results << result }
    }

    // show summary
    doLast {
        println "Tests summary"
        println "-------------------------------"
        println "Tests: ${results.sum { it.testCount }}" +
                ", Failures: ${results.sum { it.failedTestCount }}" +
                ", Errors: ${results.sum { it.exceptions.size() }}" +
                ", Skipped: ${results.sum { it.skippedTestCount }}"
    }
}